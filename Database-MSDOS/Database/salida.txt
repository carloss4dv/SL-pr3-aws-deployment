M E N U
1 - INTRODUCIR DATOS
2 - GRABAR DATOS
3 - ORDENAR DATOS
4 - INFORMACION BASE DE DATOS
5 - IMPRIMIR DATOS SEGUN ORDEN
6 - LISTAR DATOS SEGUN ORDEN
7 - BUSCAR DATOS
8 - ACABAR
LOCALIZACION DE DATOS
SABES EL NUMERO DE REGISTRO QUE QUIERES BUSCAR? (S/N) S
CUAL ES EL NUMERO DEL REGISTRO QUE QUIERES VER? (1/ 763 ) 
? 763
 763  - BYTE MASTER   UTILIDAD   CINTA:33

ES ESTE ? (S/N)  ? S
QUIERES ALTERAR SUS DATOS?  S/N  ? N
QUIERES BUSCAR ALGUNO MAS? S/N  N
M E N U
1 - INTRODUCIR DATOS
2 - GRABAR DATOS
3 - ORDENAR DATOS
4 - INFORMACION BASE DE DATOS
5 - IMPRIMIR DATOS SEGUN ORDEN
6 - LISTAR DATOS SEGUN ORDEN
7 - BUSCAR DATOS
8 - ACABAR
SEGURO QUE QUIERES SALIR ? (LOS DATOS NO GRABADOS SE PERDERAN) (S/N) 
? S
GRACIAS POR USAR (C) DATABASE
MENA SOFTWARE LTD.

Okÿ
list
1 '
2 '                *************************
3 '                **   (C) DATABASE      **
4 '                **         BY          **
5 '                **  MENA SOFTWARE LTD. **
6 '                *************************
7 '
8 '
9 '
10 SCREEN 0:KEY OFF
14 '
15 '                   LECTURA Y ASIGNACION DATOS GRABADOS
16 '
20 OPEN "INFORM.DAT" FOR INPUT AS #1
30 INPUT#1,NUM,CAMP$,ORDEN:NUMA=NUM
40 DIM N$(NUMA+100):DIM T$(NUMA+100):DIM C$(NUMA+100):DIM O$(NUMA+100)
50 FOR N=1 TO NUMA:INPUT#1,O$(N):NEXT N
60 OPEN "DATOS.DAT" FOR INPUT AS #2
80 FOR N=1 TO NUMA:INPUT#2,N$(N),T$(N),C$(N):NEXT N
90 CLOSE
97 '
98 '                    MENU
99 '
100 CLS:LOCATE 4,32:PRINT "M E N U"
110 LOCATE 7,25:PRINT "1 - INTRODUCIR DATOS"
120 LOCATE 9,25:PRINT "2 - GRABAR DATOS"
130 LOCATE 11,25:PRINT "3 - ORDENAR DATOS"
140 LOCATE 13,25:PRINT "4 - INFORMACION BASE DE DATOS"
150 LOCATE 15,25:PRINT "5 - IMPRIMIR DATOS SEGUN ORDEN"
155 LOCATE 17,25:PRINT "6 - LISTAR DATOS SEGUN ORDEN"
156 LOCATE 19,25:PRINT "7 - BUSCAR DATOS"
158 LOCATE 22,25:PRINT "8 - ACABAR"
160 A$=INKEY$:IF (A$="") OR (A$<"1") OR (A$>"8") THEN 160
170 ON VAL(A$) GOSUB 1000,2000,3000,4000,5000,6000,7000,8000
180 GOTO 100
997 '
998 '                       INTRODUCIR DATOS
999 '
1000 CLS:LOCATE 3,1:PRINT "INTRODUCCION DE DATOS"
1005 LOCATE 8,35:PRINT "FICHERO N§";NUM+1
1010 LOCATE 10,5:INPUT "NOMBRE: ",N$:IF LEN(N$)>30 THEN LOCATE 10,5:PRINT "                                                                         ":GOTO 1010
1015 IF N$="" THEN RETURN:'  VOLVER AL MENU
1020 LOCATE 13,15:PRINT "       UTILIDAD        ARCADE          CONVERSACIONAL   VIDEOAVENTURA                  SIMULADOR       JUEGO DE MESA   S. DEPORTIVO     ESTRATEGIA"
1030 LOCATE 12,5:INPUT "TIPO: ",T$:IF (LEN(T$)>15)OR(T$="") THEN LOCATE 12,5:PRINT "                                                                         ":GOTO 1030
1040 LOCATE 17,5:INPUT "CINTA: ",C$:IF (LEN(C$)>5)OR(C$="") THEN LOCATE 16,5:PRINT "                                          ":GOTO 1040
1050 LOCATE 20,35:INPUT "PULSA 'N' SI HAY ALGUN DATO INCORRECTO  ",D$:IF (D$="N")OR(D$="n") THEN 1000
1060 ORDEN=0:NUM=NUM+1:N$(NUM)=N$:T$(NUM)=T$:C$(NUM)=C$:IF ALT THEN ALT=0:RETURN ELSE O$(NUM)=RIGHT$(STR$(NUM),LEN(STR$(NUM))-1):IF NUM=NUMA+100 THEN LOCATE 20,1:PRINT "DEBES ACABAR LA INTRODUCCION DE DATOS, GRABARLO TODO Y HACER RUN":PLAY "CDCD":RETURN
1070 GOTO 1000
1997 '
1998 '                           GRABACION DE DATOS
1999 '
2000 CLS:LOCATE 2,30:PRINT "GRABACION DE DATOS"
2010 LOCATE 7,25:PRINT "1 - GRABAR FICHEROS Y ORDENACION"
2020 LOCATE 10,25:PRINT "2 - GRABAR ORDENACION"
2030 LOCATE 13,25:PRINT "3 - VOLVER AL MENU"
2040 A$=INKEY$:IF (A$="") OR (A$<"1") OR (A$>"3") THEN 2040
2050 ON VAL(A$) GOSUB 2100,2300
2060 RETURN
2097 '
2098 '                     GRABAR TODO
2099 '
2100 GOSUB 2200:GOSUB 2300:RETURN
2197 '
2198 '                     GRABAR DATOS
2199 '
2200 LOCATE 15,30:PRINT "GRABANDO LOS DATOS"
2220 OPEN "DATOS.DAT" FOR OUTPUT AS #2
2230 FOR N=1 TO NUM:WRITE#2,N$(N),T$(N),C$(N):NEXT N
2240 CLOSE:RETURN
2297 '
2298 '                       GRABAR ORDENACION
2299 '
2300 LOCATE 17,28:PRINT "GRABANDO LA ORDENACION"
2305 OPEN "INFORM.DAT" FOR OUTPUT AS #1:WRITE#1,NUM,CAMP$,ORDEN
2310 FOR N=1 TO NUM:WRITE#1,O$(N):NEXT N
2320 CLOSE:RETURN
2997 '
2998 '                          ORDENACION
2999 '
3000 CLS:LOCATE 3,35:PRINT "ORDENACION"
3010 LOCATE 10,32:PRINT "1 - NOMBRE"
3020 LOCATE 12,32:PRINT "2 - TIPO"
3030 LOCATE 14,32:PRINT "3 - CINTA"
3035 LOCATE 16,32:PRINT "4 - ANTIGUEDAD"
3040 LOCATE 21,15:INPUT "¨ SEGUN QUE CAMPO QUIERES ORDENAR LOS DATOS ? ",A$
3050 IF (A$<"1")OR(A$>"4") THEN 3040 ELSE IF A$<>"4" THEN DIM F$(NUM)
3060 IF A$="1" THEN CAMP$="NOMBRE":FOR N=1 TO NUM:F$(N)=N$(N):NEXT N
3070 IF A$="2" THEN CAMP$="TIPO":FOR N=1 TO NUM:F$(N)=T$(N):NEXT N
3080 IF A$="3" THEN CAMP$="CINTA":FOR N=1 TO NUM:F$(N)=C$(N):NEXT N
3085 IF A$="4" THEN CAMP$="ANTIGUEDAD":GOTO 3300
3120 M=LOG(NUM)/LOG(2)
3130 DIM P(M,2):X$=SPACE$(31)
3140 P=1:P(1,1)=1:P(1,2)=NUM
3150 IZ=P(P,1):DE=P(P,2):P=P-1
3160 I=IZ:J=DE:MID$(X$,1)=F$(VAL(O$((IZ+DE)/2)))
3170 IF F$(VAL(O$(I)))<LEFT$(X$,LEN(F$(VAL(O$(I))))) THEN I=I+1:GOTO 3170
3180 IF LEFT$(X$,LEN(F$(VAL(O$(J)))))<F$(VAL(O$(J))) THEN J=J-1:GOTO 3180
3190 IF I<=J THEN SWAP O$(I),O$(J):I=I+1:J=J-1
3200 IF I<=J THEN 3170
3210 IF J-IZ>=DE-I THEN 3240
3220 IF I<DE THEN P=P+1:P(P,1)=I:P(P,2)=DE
3230 DE=J:GOTO 3260
3240 IF IZ<J THEN P=P+1:P(P,1)=IZ:P(P,2)=J
3250 IZ=I
3260 IF IZ<DE THEN 3160
3270 IF P<>0 THEN 3150
3280 ERASE P,F$:ORDEN=1:INPUT B:RETURN
3300 FOR N=1 TO NUM:O$(N)=RIGHT$(STR$(N),LEN(STR$(N))-1):NEXT N:ORDEN=1:RETURN
3997 '
3998 '                        INFORMACION BASE DE DATOS
3999 '
4000 CLS:LOCATE 2,27:PRINT "INFORMACION BASE DE DATOS"
4010 LOCATE 5,1:PRINT "CONTIENE ";NUM;" ARCHIVOS"
4020 LOCATE 7,1:PRINT "LA BASE DE DATOS ESTA ";:IF ORDEN THEN PRINT "ORDENADA SEGUN EL CAMPO '";CAMP$;"'." ELSE PRINT "DESORDENADA.":PRINT "LA ULTIMA ORDENACION FUE SEGUN EL CAMPO '";CAMP$;"'."
4030 LOCATE 10,1:PRINT "MEMORIA DISPONIBLE =";FRE(0);"BYTES"
4200 LOCATE 24,1:INPUT "PULSA ENTER           ";A:RETURN
4997 '
4998 '                       IMPRIMIR DATOS
4999 '
5000 CLS:NCA=0:LOCATE 3,3:PRINT "DIME LOS CAMPOS QUE QUIERES IMPRIMIR"
5010 LOCATE 5,5:INPUT "No. DE REGISTRO?  S/N  ";A$:IF (A$="S")OR(A$="s") THEN C1=1:NCA=1 ELSE C1=0
5020 LOCATE 7,5:INPUT "NOMBRE?  S/N  ";A$:IF (A$="S")OR(A$="s") THEN C2=1:NCA=NCA+1 ELSE C2=0
5030 LOCATE 9,5:INPUT "TIPO?  S/N  ";A$:IF (A$="S")OR(A$="s") THEN C3=1:NCA=NCA+1 ELSE C3=0
5040 LOCATE 11,5:INPUT "CINTA?  S/N  ";A$:IF (A$="S")OR(A$="s") THEN C4=1:NCA=NCA+1 ELSE C4=0
5043 IF NCA=0 THEN RETURN
5045 LR=3*C1+31*C2+15*C3+5*C4+NCA-1:LP=130:AP=132:NC=INT(AP/(LR+1)):F=0
5046 IF PRIN=0 THEN DIM P$(6):P$(1)=SPACE$(57):P$(2)=SPACE$(AP):P$(3)=SPACE$(3):P$(4)=SPACE$(31):P$(5)=SPACE$(15):P$(6)=SPACE$(5):PRIN=1
5050 CLS:LOCATE 3,1:INPUT "INTRODUCE EL No. DE REGISTRO DEL PRIMER PROGRAMA O ENTER PARA IMPRIMIR TODOS    ";A$:IF (VAL(A$)<0)OR(VAL(A$)>NUM) THEN 5050
5060 IF A$="" THEN NI=1 ELSE NI=VAL(A$)
5063 CLS
5064 LOCATE 10,25:PRINT "PULSA SPACE PARA IMPRIMIR"
5065 A$=INKEY$:IF A$<>" " THEN 5065
5066 LOCATE 10,22:PRINT "PULSA ESC PARA DETENER LA IMPRESION"
5070 GOTO 5180
5080 MID$(P$(1),1,57)=SPACE$(57):P2=1:O=VAL(O$(A))
5130 IF C1 THEN MID$(P$(3),1,3)=SPACE$(3):OO$=RIGHT$(STR$(O),LEN(STR$(O))-1):MID$(P$(3),4-LEN(OO$),LEN(OO$))=OO$:MID$(P$(1),P2,3)=P$(3):P2=P2+4
5140 IF C2 THEN MID$(P$(4),1,31)=SPACE$(31):MID$(P$(4),1,LEN(N$(O)))=N$(O):MID$(P$(1),P2,31)=P$(4):P2=P2+32
5150 IF C3 THEN MID$(P$(5),1,15)=SPACE$(15):MID$(P$(5),1,LEN(T$(O)))=T$(O):MID$(P$(1),P2,15)=P$(5):P2=P2+16
5160 IF C4 THEN MID$(P$(6),1,5)=SPACE$(5):MID$(P$(6),1,LEN(C$(O)))=C$(O):MID$(P$(1),P2,5)=P$(6)
5175 RETURN
5180 '
5190 Y=0
5200 MID$(P$(2),1,AP)=SPACE$(AP):P1=1
5210 FOR N=1 TO NC
5220 A=NI+(N-1)*LP
5230 IF A<=NUM THEN GOSUB 5080:MID$(P$(2),P1,LR)=LEFT$(P$(1),LR+NCA-1):P1=P1+LR+1
5240 IF A>NUM THEN F=1
5245 NEXT N
5250 WIDTH "LPT1:",132:LPRINT CHR$(27);"3";CHR$(14);CHR$(27);"S";CHR$(0);CHR$(15);LEFT$(P$(2),LR*NC+NC-1);:Y=Y+1
5260 IF Y<LP THEN NI=NI+1:A$=INKEY$:IF A$<>CHR$(27) THEN GOTO 5200 ELSE GOTO 5300:' SIGUIENTE LINEA
5270 IF F=0 THEN PRINT:CLS:NI=NI+(NC-1)*LP+1:LOCATE 20,1:LPRINT CHR$(8):PRINT "FIN DE PAGINA. INTRODUCE OTRA HOJA EN LA IMPRESORA Y PULSA ENTER   ";:INPUT A$:LOCATE 20,1:PRINT SPACE$(80):GOTO 5063:' SIGUIENTE PAGINA
5280 RETURN
5300 LOCATE 20,25:LPRINT CHR$(8):PRINT "ENTER PARA VOLVER AL MENU":LOCATE 22,25:PRINT "SPACE PARA CONTINUAR LA IMPRESION"
5310 A$=INKEY$:IF A$="" THEN 5310
5320 IF A$=CHR$(13) THEN GOTO 5280
5330 IF A$=" " THEN LOCATE 20,1:PRINT SPACE$(80):LOCATE 22,1:PRINT SPACE$(80):LOCATE 10,22:PRINT "PULSA ESC PARA DETENER LA IMPRESION":GOTO 5200
5340 GOTO 5310
5997 '
5998 '                    LISTAR DATOS SEGUN EL ORDEN
5999 '
6000 CLS:LOCATE 1,27:PRINT "LISTADO DE DATOS SEGUN EL ORDEN"
6003 LOCATE 5,1:PRINT "ORDENACION SEGUN EL CAMPO=";CAMP$:PRINT:PRINT "INTRODUCE EL DATO DEL PRIMER PROGRAMA A VISUALIZAR                              O ENTER PARA VER DESDE EL PRINCIPIO":LOCATE 10,30:INPUT N$
6004 IF N$="" THEN N=1:GOTO 6011
6005 IF (CAMP$="ANTIGUEDAD")AND(N$<=RIGHT$(STR$(NUM),LEN(STR$(NUM))-1)) THEN N=VAL(N$):GOTO 6011
6006 IF (N$>RIGHT$(STR$(NUM),LEN(STR$(NUM))-1))AND(CAMP$="ANTIGUEDAD") THEN RETURN
6007 FOR K=1 TO NUM:Q=VAL(O$(K)):IF CAMP$="NOMBRE" THEN IF INSTR(N$(Q),N$)<>0 THEN N=K:GOTO 6011
6008 IF CAMP$="TIPO" THEN IF INSTR(T$(Q),N$)<>0 THEN N=K:GOTO 6011
6009 IF CAMP$="CINTA" THEN IF INSTR(C$(Q),N$)<>0 THEN N=K:GOTO 6011
6010 NEXT K:PRINT "ESE PROGRAMA NO EXISTE":FOR L=1 TO 500:NEXT L:RETURN
6011 CLS:LOCATE 3,1:PRINT "      N§    NOMBRE                           TIPO              CINTA    REGISTRO";
6015 Y=5
6020 N$=STR$(N):ORD$=O$(N):ORD=VAL(ORD$):LOCATE Y,9-LEN(N$):PRINT N$:LOCATE Y,13:PRINT N$(ORD):LOCATE Y,46:PRINT T$(ORD):LOCATE Y,66:PRINT C$(ORD):LOCATE Y,77-LEN(ORD$):PRINT ORD
6025 Y=Y+1:N=N+1
6027 IF (Y<23)AND(N<NUM+1) THEN 6020
6030 LOCATE 25,1:PRINT "PULSA SPACE PARA CONTINUAR U OTRA TECLA PARA ACABAR";
6040 A$=INKEY$:IF A$="" THEN 6040
6050 IF (A$=" ")AND(N<NUM+1) THEN GOTO 6011
6060 RETURN
6997 '
6998 '          BUSCAR DATOS
6999 '
7000 CLS:LOCATE 1,30:PRINT "LOCALIZACION DE DATOS"
7010 LOCATE 5,1:INPUT "SABES EL NUMERO DE REGISTRO QUE QUIERES BUSCAR? (S/N) ",A$:IF (A$<>"S") AND (A$<>"N") THEN 7010
7015 IF A$="N" THEN 7040
7020 LOCATE 10,1:PRINT "CUAL ES EL NUMERO DEL REGISTRO QUE QUIERES VER? (1/";NUM;") ":INPUT N:IF (N<1)OR(N>NUM) THEN 7020
7030 GOTO 7060
7040 LOCATE 10,1:INPUT "DIME EL NOMBRE DEL PROGRAMA QUE QUIERES VER     ",N$:N=1
7050 IF INSTR(N$(N),N$)=0 THEN N=N+1:IF N>NUM THEN LOCATE 12,1:PRINT "NO HAY NINGUN PROGRAMA CON ESE NOMBRE; PULSA ENTER":INPUT A:GOTO 7080 ELSE GOTO 7050
7060 CLS:LOCATE 5,5:PRINT N;" - ";N$(N);"   ";T$(N);"   CINTA:";C$(N);
7062 PRINT:PRINT:INPUT "ES ESTE ? (S/N)  ";A$:IF (A$="N")OR(A$="n") THEN N=N+1:GOTO 7050
7065 LOCATE 20,1:INPUT "QUIERES ALTERAR SUS DATOS?  S/N  ";A$:IF (A$<>"S")AND(A$<>"s") THEN GOTO 7080
7067 LOCATE 20,1:PRINT "                                       ";
7070 ALT=1:NUM0=NUM:NUM=N-1:GOSUB 1005:NUM=NUM0
7080 CLS:LOCATE 10,10:INPUT "QUIERES BUSCAR ALGUNO MAS? S/N  ",A$:IF (A$<>"S")AND(A$<>"s") THEN RETURN
7090 GOTO 7000
8000 CLS:PRINT "SEGURO QUE QUIERES SALIR ? (LOS DATOS NO GRABADOS SE PERDERAN) (S/N) "
8010 INPUT A$
8015 IF A$<>"S" THEN RETURN
8020 CLS:PRINT "GRACIAS POR USAR (C) DATABASE"
8030 PRINT"MENA SOFTWARE LTD.":PRINT
8040 END
8500 NUM=10:DIM A$(NUM):DIM O$(NUM):FOR N=1 TO NUM:A$(N)=CHR$(INT(RND*20+65)):O$(N)=STR$(N):NEXT N
8900 FOR N=1 TO NUM:PRINT A$(VAL(O$(N)));"  ";:NEXT N:PRINT
8997 '
8998 '
8999 '
9000 M=LOG(NUM)/LOG(2)
9010 DIM P(M,2)
9020 P=1:P(1,1)=1:P(1,2)=NUM
9030 IZ=P(P,1):DE=P(P,2):P=P-1
9040 I=IZ:J=DE:X$=A$(VAL(O$((IZ+DE)/2)))
9050 IF A$(VAL(O$(I)))<X$ THEN I=I+1:GOTO 9050
9060 IF X$<A$(VAL(O$(J))) THEN J=J-1:GOTO 9060
9070 IF I<=J THEN SWAP O$(I),O$(J):I=I+1:J=J-1
9080 IF I<=J THEN 9050
9090 IF J-IZ>=DE-I THEN 9120
9100 IF I<DE THEN P=P+1:P(P,1)=I:P(P,2)=DE
9110 DE=J:GOTO 9140
9120 IF IZ<J THEN P=P+1:P(P,1)=IZ:P(P,2)=J
9130 IZ=I
9140 IF IZ<DE THEN 9040
9150 IF P<>0 THEN 9030
9160 ERASE P
9600 FOR N=1 TO NUM:PRINT A$(VAL(O$(N)));"  ";:NEXT N
10010 FOR N=1 TO 70:MID$(F$,N,10)="A":NEXT N:PRINT FRE(0):GOTO 10010
Okÿ
run
M E N U
1 - INTRODUCIR DATOS
2 - GRABAR DATOS
3 - ORDENAR DATOS
4 - INFORMACION BASE DE DATOS
5 - IMPRIMIR DATOS SEGUN ORDEN
6 - LISTAR DATOS SEGUN ORDEN
7 - BUSCAR DATOS
8 - ACABAR
